apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'net.minecrell.licenser'

// Basic information
group = 'org.lanternpowered'

// Set java version
sourceCompatibility = 1.8
targetCompatibility = 1.8

// Setup the default tasks
defaultTasks 'licenseFormat', 'build'

// Environment variables for the build set by the build server
ext {
    buildNumber = System.env.BUILD_NUMBER ?: System.env.TRAVIS_BUILD_NUMBER ?: '0'
    ciSystem = System.env.CI_SYSTEM ?: (System.env.TRAVIS ? 'travis' : null)
    commit = System.env.GIT_COMMIT ?: System.env.TRAVIS_COMMIT
    branch = System.env.GIT_BRANCH ?: System.env.TRAVIS_BRANCH
    api = rootProject.project('SpongeAPI')
}

// IntelliJ IDEA resource fix
idea.module.inheritOutputDirs = true

// Repositories
repositories {
    mavenCentral()
    maven { url 'https://repo.spongepowered.org/maven' }
    maven { url 'https://oss.sonatype.org/content/groups/public' }
}

if (!api.projectDir.exists() || api.projectDir.list().length == 0) {
    String part = api.projectDir.exists() ? 'empty' : 'missing'
    throw new IllegalStateException("The SpongeAPI project appears to be $part, was it cloned properly?")
}
evaluationDependsOn api.path

// Process the resource files
processResources {
    // Note: Do not use this method to remove resources, rename them, etc. this
    // may lead to some unintentional side effects that resources can't be found.

    // Include the license file
    from rootProject.file('LICENSE.txt')
}

// A task to generate a jar with all the compiled classes and resources
jar {
    classifier = ''
    duplicatesStrategy = 'EXCLUDE'
}

// A task to generate a jar with all the source code and resources
task sourceJar(type: Jar) {
    classifier = 'sources'
    duplicatesStrategy = 'EXCLUDE'

    from sourceSets.main.allSource
    // Include the license file
    from rootProject.file('LICENSE.txt')
}
assemble.dependsOn sourceJar

license {
    header rootProject.file('HEADER.txt')
    include '**/*.java'
    include '**/*.kt'

    newLine false
    ignoreFailures false

    ext {
        name = rootProject.name
        urla = rootProject.url
        organizationa = rootProject.organization
        urlb = rootProject.api.url
        organizationb = rootProject.api.organization
    }
}

// Don't generate artifacts for the SpongeAPI
api.configurations.archives.artifacts.clear()
