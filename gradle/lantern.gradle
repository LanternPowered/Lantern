apply from: rootProject.file('gradle/lantern-base.gradle')

apply plugin: 'java'
apply plugin: 'org.spongepowered.plugin'
apply plugin: 'com.github.hierynomus.license'

// Setup the default tasks
defaultTasks 'licenseFormat', 'build'

// Repositories
repositories {
    mavenCentral()
    maven { url 'https://repo.spongepowered.org/maven' }
    maven { url 'https://oss.sonatype.org/content/groups/public' }
}

ext.api = rootProject.project('SpongeAPI')
if (!api.projectDir.exists() || api.projectDir.list().length == 0) {
    String part = api.projectDir.exists() ? 'empty' : 'missing'
    throw new IllegalStateException("The SpongeAPI project appears to be $part, was it cloned properly?")
}
evaluationDependsOn api.path

// Process the resource files
processResources {
    // Note: Do not use this method to remove resources, rename them, etc. this
    // may lead to some unintentional side effects that resources can't be found.

    // Include the license file
    from rootProject.file('LICENSE.txt')
}

// A task to generate a jar with all the compiled classes and resources
jar {
    classifier = ''
    duplicatesStrategy = 'EXCLUDE'
}

// A task to generate a jar with all the source code and resources
task sourceJar(type: Jar) {
    classifier = 'sources'
    duplicatesStrategy = 'EXCLUDE'

    from sourceSets.main.allSource
    // Include the license file
    from rootProject.file('LICENSE.txt')
}
assemble.dependsOn sourceJar

license {
    header rootProject.file('HEADER.txt')
    include '**/*.java'

    strictCheck true
    ignoreFailures = false

    ext {
        name = rootProject.name
        urla = rootProject.url
        organizationa = rootProject.organization
        urlb = rootProject.api.url
        organizationb = rootProject.api.organization
    }

    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

// Don't generate artifacts for the SpongeAPI
api.configurations.archives.artifacts.clear()
